buildscript {
	repositories {
		mavenCentral()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven/"
		}
		maven {
			name "sonatype"
			url "https://oss.sonatype.org/content/repositories/snapshots"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
	}
}

apply plugin: "forge"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = mc_version + "-" + mod_version

archivesBaseName = "DiscordChat"

minecraft {
	version = mc_version + "-" + forge_version
	runDir = "run"

	mappings = mcp_mappings

	replaceIn "OneSevenTenMod.java"
	replace "@VERSION@", mod_version

	srgExtra "PK: com/mashape/unirest net/shadowfacts/discordchat/repack/com/mashape/unirest"
	srgExtra "PK: com/neovisionaries/ws/client net/shadowfacts/discordchat/repack/com/neovisionaries/ws/client"
	srgExtra "PK: com/sun/jna net/shadowfacts/discordchat/repack/com/sun/jna"
	srgExtra "PK: javax/sound net/shadowfacts/discordchat/repack/javax/sound"
	srgExtra "PK: javazoom net/shadowfacts/discordchat/repack/javazoom"
	srgExtra "PK: net/dv8tion/jda net/shadowfacts/discordchat/repack/net/dv8tion/jda"
	srgExtra "PK: net/sourceforge/jaad net/shadowfacts/discordchat/repack/net/sourceforge/jaad"
	srgExtra "PK: org/apache/commons/codec net/shadowfacts/discordchat/repack/org/apache/commons/codec"
	srgExtra "PK: org/apache/commons/lang3 net/shadowfacts/discordchat/repack/org/apache/commons/lang3"
	srgExtra "PK: org/json net/shadowfacts/discordchat/repack/org/json"
	srgExtra "PK: org/kc7bfi/jflac net/shadowfacts/discordchat/repack/orgkc7bfi/jflac"
	srgExtra "PK: org/tritonus net/shadowfacts/discordchat/repack/org/tritonus"
	srgExtra "PK: tomp2p/opuswrapper net/shadowfacts/discordchat/repack/tomp2p/opuswrapper"

	srgExtra "PK: org/apache/http net/shadowfacts/discordchat/repack/org/apache/http"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

configurations {
	shade
	compile.extendsFrom shade
}

repositories {
	maven {
		name "shadowfacts"
		url "http://mvn.rx14.co.uk/shadowfacts/"
	}
	flatDir {
		dirs "../core/libs"
	}
}

dependencies {
	shade project(":core")
}

jar {
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude "META-INF", "META-INF/**"
		}
	}
}