buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
	}
}

plugins {
	id "com.matthewprenger.cursegradle" version "1.0.6"
	id "com.github.johnrengelman.shadow" version "1.2.2"
}

apply plugin: "net.minecraftforge.gradle.forge"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = mc_version + "-" + mod_version

minecraft {
	version = mc_version + "-" + forge_version
	runDir = "run"

	mappings = mcp_mappings
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

repositories {
	jcenter()
	maven {
		name "shadowfacts"
		url "http://mvn.rx14.co.uk/shadowfacts"
	}
}

dependencies {
	compile group: "net.shadowfacts", name: "ShadowMC", version: mc_version + "-" + shadowmc_version

	compile group: "net.dv8tion", name: "JDA", version: jda_version
}

shadowJar {
	classifier = ""
	dependencies {
		include(dependency("net.dv8tion:JDA:1.2.1_106"))
	}
}

curseforge {
	apiKey = project.hasProperty("curseForgeApiKey") ? project.curseForgeApiKey : ""

	project {
		id = "240995"

		addGameVersion "1.8.8"
		addGameVersion "1.8.9"
		addGameVersion "Java 8"

		mainArtifact(shadowJar) {
			releaseType = "release"
			changelog = ""

			relations {
				requiredLibrary "shadowmc"
			}
		}
	}
}
reobf { shadowJar { mappingType = "SEARGE" } }
tasks.reobfShadowJar.mustRunAfter shadowJar
afterEvaluate {
	tasks.curseforge240995.dependsOn reobfShadowJar
}